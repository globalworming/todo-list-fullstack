plugins {
    id "java"
    id "application"
    id 'com.google.protobuf' version '0.8.18'
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id 'com.google.cloud.tools.jib' version '3.3.1'
}

mainClassName = 'com.headissue.compliance.ServletApplication'
group 'com.headissue.compliance'
version '1.0-SNAPSHOT'

ext {
    jettyVersion = '11.0.12'
    grpcVersion = '1.51.0'
}

repositories {
    mavenCentral()
}

dependencies {
    // server and servlets
    implementation "org.eclipse.jetty:jetty-server:${jettyVersion}"
    implementation "org.eclipse.jetty:jetty-servlet:${jettyVersion}"

    // write to and parse from json
    implementation 'com.google.code.gson:gson:2.10'

    // grpc
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    compileOnly 'org.apache.tomcat:annotations-api:6.0.53'
    runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"

    //logging
    implementation 'ch.qos.logback:logback-classic:1.4.5'
    implementation 'com.google.cloud:google-cloud-logging-logback:0.129.3-alpha'
    testImplementation 'io.rest-assured:rest-assured:5.3.0'
    testImplementation 'org.awaitility:awaitility:4.2.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.8.2'
    testImplementation 'org.mockito:mockito-core:5.0.0'
}


sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', 'build/generated/source/proto/main/java', 'build/generated/source/proto/main/grpc']
        }
        proto {
            srcDirs = ['../grpc']
        }
    }

    test {
        java {
            srcDirs = ['src/test/java']
        }
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.21.7"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.51.0'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

shadowJar {
    mergeServiceFiles()
}

jib {
    to {
        image = 'gcr.io/PROJECT_ID/bff'
        credHelper = 'gcr'
    }
}


test {
    useJUnitPlatform()

    testLogging {
        events("passed", "skipped", "failed")
        exceptionFormat = org.gradle.api.tasks.testing.logging.TestExceptionFormat.FULL
    }
}